// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y–x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs.
 * Which function to compute is determined by 6 input bits 
 * denoted zx, nx, zy, ny, f, no.
 * The computed function's value is called "out".
 * In addition to computing out, the ALU computes two 
 * 1-bit outputs called zr and ng:
 * if out == 0, zr = 1; otherwise zr = 0;
 * If out < 0, ng = 1; otherwise ng = 0.
 * The 6-bit combinations (zx,nx,zy,ny,f,no) and 
 * their effect are documented in the book. 
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx  == 1) sets x = 0        // 16-bit constant
// if (nx  == 1) sets x = ~x       // bitwise "not"
// if (zy  == 1) sets y = 0        // 16-bit constant
// if (ny  == 1) sets y = ~y       // bitwise "not"
// if (f   == 1) sets out = x + y  // integer 2's-complement addition
// if (f   == 0) sets out = x & y  // bitwise And
// if (no  == 1) sets out = ~out   // bitwise Not
// if (out == 0) sets zr = 1
// if (out < 0)  sets ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if f == 1) or out = x & y (if == 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
	Mux16(a=x,b=false,sel=zx,out=resx1);
	Not16(in=resx1,out=resx2);
	Mux16(a=resx1,b=resx2,sel=nx,out=resxf);
	
	Mux16(a=y,b=false,sel=zy,out=resy1);
	Not16(in=resy1,out=resy2);
	Mux16(a=resy1,b=resy2,sel=ny,out=resyf);
	
	Add16(a=resxf,b=resyf,out=addxy);
	And16(a=resxf,b=resyf,out=andxy);
	
	Mux16(a=andxy,b=addxy,sel=f,out=resxy);
	Not16(in=resxy,out=nresxy);
	Mux16(a=resxy,b=nresxy,sel=no,out=out,
				out[0..7]=nout1,out[8..15]=nout2,out[15]=sign);
	
	Or8Way(in=nout1,out=zr1);
	Or8Way(in=nout2,out=zr2);
	Not(in=zr1,out=nzr1);
	Not(in=zr2,out=nzr2);
	And(a=nzr1,b=nzr2,out=zr);
	
	And(a=sign,b=true,out=ng);
}
